// Top-level build file where you can add configuration options common to all sub-projects/modules.

apply plugin: 'io.codearte.nexus-staging'

ext {
  minSdkVersion = 23
  compileSdkVersion = 28
  buildToolsVersion = '28.0.3'
}

ext.deps = [ appcompat  : 'androidx.appcompat:appcompat:1.1.0',
             material   : 'com.google.android.material:material:1.1.0',
             junit      : 'junit:junit:4.13',
             truth      : 'com.google.truth:truth:1.0.1',
             mockito    : 'org.mockito:mockito-core:3.4.0']

buildscript {
  repositories {
    google()
    jcenter()
    mavenCentral()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:3.6.3'
    classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.2"

    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

allprojects {
  repositories {
    google()
    jcenter()
    mavenCentral()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
}

def getRepositoryUsername() {
  return hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
  return hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
}

nexusStaging {
  // optional if packageGroup == project.getGroup()
  packageGroup = GROUP
  // when not defined will be got from server using "packageGroup"
  // get it via ./gradlew getStagingProfile
  stagingProfileId = "9add401d06ecc9"
  username = getRepositoryUsername()
  password = getRepositoryPassword()
}
